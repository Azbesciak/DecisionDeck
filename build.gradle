buildscript {
    ext {
        kotlin_version = '1.3.11'
    }

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
    }
}

wrapper {
    jarFile = "./gradle/wrapper/gradle-wrapper.jar"
    gradleVersion = '4.8.1'
}

subprojects {
    group 'pl.poznan.put'
    version '1.0'

    apply plugin: 'kotlin'
    apply plugin: 'application'
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        compile files("../libs/XMCDA.jar")
        compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.4.0-b180830.0359'
        compile group: 'com.sun.activation', name: 'javax.activation', version: '1.2.0'
        compile group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.4.0-b180830.0438'
        compile group: 'com.sun.istack', name: 'istack-commons-runtime', version: '3.0.7'
        if (project.name != "utils")
            compile project(':utils')
        if (project.name.startsWith("topsis")) {
            testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.0-M1'
            if (project.name != "topsis-common")
                compile project(':topsis-common')
        }
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
}

task genExecutables {
    def runnableJVMProjects = [
          "ahp", "topsis", "topsis-distance-ranking", "topsis-ideal-alternatives", "topsis-normalization-weighting"
    ]
    runnableJVMProjects.each { proj ->
        copy {
            from 'sh-templates/jvm/'
            into proj
            filter { line -> line.replaceAll("\\{project}", proj) }
        }
    }
}
